import org.jetbrains.compose.ExperimentalComposeLibrary
import org.jetbrains.compose.desktop.application.dsl.TargetFormat
import org.jetbrains.compose.reload.ComposeHotRun
import org.jetbrains.kotlin.compose.compiler.gradle.ComposeFeatureFlag
import org.jetbrains.kotlin.gradle.plugin.KotlinSourceSetTree

plugins {
    alias(libs.plugins.multiplatform)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.compose)
    alias(libs.plugins.android.application)
    alias(libs.plugins.hotReload)
    alias(libs.plugins.kotlinx.serialization)
    alias(libs.plugins.spotless)
}

kotlin {
    androidTarget {
        //https://www.jetbrains.com/help/kotlin-multiplatform-dev/compose-test.html
        instrumentedTestVariant.sourceSetTree.set(KotlinSourceSetTree.test)
    }

    jvm()

    sourceSets {
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)
            implementation(libs.kotlinx.coroutines.core)
            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.client.serialization)
            implementation(libs.ktor.client.logging)
            implementation(libs.voyager.navigator)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.voyager.transitions)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.koin.compose)
            implementation(libs.mvvm.compose)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.kotlinx.datetime)
            implementation(libs.multiplatform.settings.no.arg)
            implementation(libs.ktor.client.cio)
            implementation(libs.ktor.client.okhttp)
        }

        commonTest.dependencies {
            implementation(kotlin("test"))
            @OptIn(ExperimentalComposeLibrary::class)
            implementation(compose.uiTest)
            implementation(libs.kotlinx.coroutines.test)
        }

        androidMain.dependencies {
            implementation(compose.uiTooling)
            implementation(libs.androidx.activityCompose)
            implementation(libs.kotlinx.coroutines.android)
            implementation(libs.ktor.client.okhttp)
            implementation(libs.libvlc.android)
            implementation(libs.koin.android)
            implementation("org.mozilla.geckoview:geckoview:130.0.20240913135723")
            implementation("com.google.android.gms:play-services-location:21.0.1")
        }

        jvmMain.dependencies {
            implementation(compose.desktop.currentOs)
            implementation(libs.kotlinx.coroutines.swing)
            implementation(libs.ktor.client.okhttp)
            implementation(libs.vlcj)

        }

    }
}


// –ø—Ä–æ—Å—Ç–∞–≤–ª–µ–Ω–∏–µ –∫–æ–æ–ø–µ—Ä–∞–π—Ç–∞ –≤ kt —Ñ–∞–π–ª—ã
// –∑–∞–ø—É—Å–∫ ./gradlew :composeApp:spotlessApply
spotless {
    // Kotlin: –¢–û–õ–¨–ö–û —à–∞–ø–∫–∞, –Ω–∏–∫–∞–∫–∏—Ö –ø—Ä–æ–≤–µ—Ä–æ–∫
    kotlin {
        target("**/*.kt")
        licenseHeader(
            """
            /*
             * Copyright (c) LLC "Centr Distribyucii"
             * All rights reserved.
             */
            """.trimIndent(),
            // –≤—Å—Ç–∞–≤–ª—è—Ç—å –ø–µ—Ä–µ–¥ –ø–µ—Ä–≤—ã–º package|import
            "(package|import)"
        )
    }

    // Java: –¢–û–õ–¨–ö–û —à–∞–ø–∫–∞
    java {
        target("**/*.java")
        licenseHeader(
            """
            /*
             * Copyright (c) LLC "Centr Distribyucii"
             * All rights reserved.
             */
            """.trimIndent()
        )
    }

    // XML: –¢–û–õ–¨–ö–û —à–∞–ø–∫–∞ –¥–ª—è layout/values/xml (–∏—Å–∫–ª—é—á–∞–µ–º mipmap/drawable/navigation/color/anim, —Ç.–∫. —Ç–∞–º —á–∞—Å—Ç–æ –Ω–µ—Ç –ø–æ–¥—Ö–æ–¥—è—â–µ–≥–æ –º–µ—Å—Ç–∞)
    format("xml-license") {
        target(
            "**/src/**/res/layout/**/*.xml",
            "**/src/**/res/values/**/*.xml",
            "**/src/**/res/xml/**/*.xml"
        )
        targetExclude(
            "**/src/**/res/mipmap*/**",
            "**/src/**/res/drawable*/**",
            "**/src/**/res/navigation/**",
            "**/src/**/res/color/**",
            "**/src/**/res/anim*/**"
        )
        licenseHeader(
            """
            <!--
              Copyright (c) LLC "Centr Distribyucii"
              All rights reserved.
            -->
            """.trimIndent(),
            // –≤—Å—Ç–∞–≤–∏—Ç—å –∫–æ–º–º–µ–Ω—Ç –ø—Ä—è–º–æ –ø–µ—Ä–µ–¥ –ø–µ—Ä–≤—ã–º —Ç–µ–≥–æ–º (—Å —É—á—ë—Ç–æ–º –≤–æ–∑–º–æ–∂–Ω–æ–≥–æ <?xml ...?>)
            "(?s)\\A\\s*(?:<\\?xml.*?\\?>\\s*)?(?=<)"
        )
    }

    // –ü—Ä–æ—á–µ–µ
    format("misc") {
        target("**/*.gradle.kts", "**/*.md", "**/.gitignore")
        trimTrailingWhitespace()
        endWithNewline()
    }
}


android {
    namespace = "videoTrade.screonPlayer.app"
    compileSdk = 35

    defaultConfig {
        minSdk = 21
        targetSdk = 35
        resValue("string", "app_name", "Screon Player")


        applicationId = "videoTrade.screonPlayer.app.androidApp"
        versionCode = 1
        versionName = "1.0.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            isMinifyEnabled = true           // –æ–±—Ñ—É—Å–∫–∞—Ü–∏—è + —Å–∂–∞—Ç–∏–µ –∫–æ–¥–∞
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-android.pro"
            )

            resValue("string", "app_name", "ScreonPlayer") // –∏–º—è –ø—Ä–∏–ª–æ–∂–µ–Ω–∏—è
        }
        debug {
            // –ß—Ç–æ–±—ã –Ω–∞ dev-—É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞—Ö –æ—Ç–ª–∏—á–∞–ª–æ—Å—å
            resValue("string", "app_name", "ScreonPlayer (debug)")
        }
    }
}

//https://developer.android.com/develop/ui/compose/testing#setup
dependencies {
    androidTestImplementation(libs.androidx.uitest.junit4)
    debugImplementation(libs.androidx.uitest.testManifest)
}

compose.desktop {
    application {
        mainClass = "MainKt"

        nativeDistributions {
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
            packageName = "screon"
            packageVersion = "1.0.0"
            vendor         = "Videotrade"

            windows {
                iconFile.set(project.file("desktopAppIcons/WindowsIcon.ico"))
                shortcut = true
                menuGroup = "screon"
                includeAllModules = true
                packageName = "screon"
            }

            modules("java.management")
        }

    }
}



//afterEvaluate {
//    val vlcSource = File(rootProject.projectDir, "composeApp/src/jvmMain/vlc")
//    val appDirProvider = layout.buildDirectory.dir("compose/binaries/main/app")
//
//    tasks.named("createDistributable") {
//        doLast {
//            val appDir = appDirProvider.get().asFile
//            val targetDir = File(appDir, "vlc")
//
//            if (vlcSource.exists()) {
//                vlcSource.copyRecursively(targetDir, overwrite = true)
//                println("‚úÖ VLC —Å–∫–æ–ø–∏—Ä–æ–≤–∞–Ω –≤ distributable: ${targetDir.absolutePath}")
//            } else {
//                println("‚ö†Ô∏è VLC –Ω–µ –Ω–∞–π–¥–µ–Ω: ${vlcSource.absolutePath}")
//            }
//        }
//    }
//}



//https://github.com/JetBrains/compose-hot-reload
composeCompiler {
    featureFlags.add(ComposeFeatureFlag.OptimizeNonSkippingGroups)
}
tasks.withType<ComposeHotRun>().configureEach {
    mainClass.set("MainKt")
}

tasks.register<Copy>("copyVlc") {
    from("$projectDir/src/jvmMain/resources/vlc")
    into("$buildDir/compose/binaries/main/app/screon/vlc")
    includeEmptyDirs = true
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    doFirst {
        println("üì¶ –ö–æ–ø–∏—Ä—É–µ–º –≤—Å—é –ø–∞–ø–∫—É VLC –≤ build/compose/binaries/main/app/screon/vlc")
    }
}
tasks.matching { it.name == "createDistributable" }.configureEach {
    finalizedBy("copyVlc")
}

tasks.withType<JavaExec> {
    systemProperty("java.library.path", file("src/jvmMain/resources/vlc").absolutePath)
}
